#!/usr/bin/perl -w

use strict;
use warnings;
use File::Temp qw/ tempfile tempdir /;
use File::Copy;
use File::Basename;
use File::Spec;
use Cwd;

use lib '.perl';

use mizar;

# this is required to untaint backticks - 
# not sure if we will go all the way to untainting
$ENV{"PATH"} = "/usr/local/sbin:/usr/local/bin:/usr/sbin:/usr/bin:/sbin:/bin";

sub pc_pad {
  my $str = shift;
  chomp $str;
  return ("[pre-commit] $str\n");
}

# I think there's a slicker way to do this, using output filters, but
# for now let's just do this as a subroutine.
sub pc_print {
  my $str = shift;
  chomp ($str); # in case it already had some extra whitespace at the end
  print (pc_pad ($str . "\n"));
}

my $wikilock;

sub pc_die
{
    pc_print(@_);
    die();
}


my $cwd = getcwd ();

# untaint 
if($cwd =~ /(.*)/) { $cwd = $1; }  

# Compute list of itp files that belong to the index (i.e., the
# itp files that are about to be committed). git diff-index will
# give a list of changed files.  We compare the index to HEAD, the
# most recent commit known to be valid/coherent.  The actual file name
# is the second field of the line (where the field separator is tab),
# hence the first call to "cut -f 2".  The last cut gives us
# "semiring" from "semiring.v" and "polyform" from "polyform.miz".

my $mizar_article_ext = 'miz';
my $coq_article_ext = 'v';
my $article_ext = $mizar_article_ext;
my $article_regexp = '\.$article_ext\$';

# Other file extensions that we have to allow.
my $mizar_special_ext = 'voc';
my $special_ext = $mizar_special_ext;
my $special_regexp = '\.$special_ext\$';

# Articles changed.
# We do not check if they are inside the "lib" directory here yet.
my @art_changed 
  = `git diff-index --cached HEAD | cut -f 2 | grep '\.$article_ext\$'`;
my @special_changed 
  = `git diff-index --cached HEAD | cut -f 2 | grep '\.$special_ext\$'`;
my @non_art_changed 
  = `git diff-index --cached HEAD | cut -f 2 | grep --invert-match '\.$article_ext\$' | grep --invert-match '\.$special_ext\$'`;


unless (scalar (@non_art_changed) == 0) {
    pc_print ("Only .$article_ext and .$special_ext files are permitted to be committed: \"$non_art_changed[0]\"  Sorry.");
  exit (1)
}

my @lib_changed = ();
my @dict_changed = ();

# Get a value for a mwiki variable either from environment (uppercase)
# or from git config (lower case).
# The input is expected as lower case.
sub get_mwiki_var
{
    my ($varname) = @_;
    my $result;

    if (defined ($ENV{uc($varname)})) 
    {
	$result = $ENV{uc($varname)};
    } 
    else 
    {
	my $git_output = `git config mwiki.$varname`;
	my $git_exit_code = ($? >> 8);
	if ($git_exit_code == 0) 
	{
	    chomp($git_output);
	    $result = $git_output;
	}
	else { return undef; }
    }
    return $result;
}

# MAKEJOBS: number of different make jobs to run simultaneously
my $makejobs = 1;
my $tmp1 =  get_mwiki_var("makejobs");
$makejobs = $tmp1 if( defined $tmp1 );


# ALLOW_SKIPPED_PROOFS: if 0, skipped proofs (@proof) are not allowed, otherwise yes
my $allow_skipped_proofs  = 1;
$tmp1 =  get_mwiki_var("allowskippedproofs");
$allow_skipped_proofs = $tmp1 if( defined $tmp1 );

# WIKIHOST: our url
my $wikihost = get_mwiki_var("wikihost");

my @repo_path_dirs = File::Spec->splitdir ($cwd);
my $repo_name = $repo_path_dirs[$#repo_path_dirs];
my $main_repo = $cwd;
my $compiled = $cwd . "/" . "../compiled";
my $sandbox = $cwd . "/" . "../sandbox";
my $sandbox_mml = $sandbox . "/" . "mml";

# Set MIZFILES appropriately
mizar::set_MIZFILES ($main_repo);

sub clean_the_dirty_sandbox {
  my $rsync_output = `rsync -a --del $compiled/ $sandbox 2>&1`;
  my $rsync_exit_code = ($? >> 8);
  unless ($rsync_exit_code == 0) {
    pc_print ("rsync did not exit cleanly when cleaning the dirty sandbox: $!");
    pc_print ("It's output was:");
    pc_print ("$rsync_output\n");
    pc_print ("We cannot continue.");
    exit 1;
  }
}

sub separate_mml_from_non_mml {
  foreach my $edited (@art_changed, @special_changed) {
    chomp ($edited);
    my ($name, $path, $extension) = fileparse ($edited, qr/\.[^.]+/);
    if (($path eq "mml/") && ($extension eq ".$article_ext")) {
	push (@lib_changed, $name);
    }
    elsif (($path eq "dict/") && ($extension eq ".$special_ext")) {
	push (@dict_changed, $name);
    } else {
      pc_print ("You are committing a file ($edited) outside the mml and dict subdirectory; we don't handle this case yet");
      exit 1;
    }
  }
}

sub copy_new_articles_to_sandbox {
  foreach my $article (@lib_changed) {
    my $real_article_path = "$cwd/mml/$article.miz";
    my $sandbox_article_path = "$sandbox_mml/$article.miz";
    copy ($real_article_path, $sandbox_article_path)
      or die ("You can't copy $real_article_path to $sandbox_article_path!");
  }
  foreach my $article (@dict_changed) {
    my $real_article_path = "$cwd/dict/$article.voc";
    my $sandbox_article_path = "$sandbox/dict/$article.voc";
    copy ($real_article_path, $sandbox_article_path)
      or die ("You can't copy $real_article_path to $sandbox_article_path!");
  }
}

# Set up PATH and MIZBIN
if (!defined ($ENV{"MIZBIN"}) or -z $ENV{"MIZBIN"}) {
  $ENV{"MIZBIN"} = $sandbox . "/bin";
}
$ENV{"PATH"} = $ENV{"PATH"} . ":" . $ENV{"MIZBIN"};


separate_mml_from_non_mml ();
clean_the_dirty_sandbox ();
copy_new_articles_to_sandbox ();

sub ensure_existence {
  my $path = shift;
  if (-e $path) {
    return $path;
  } else {
    die "There's nothing at $path, I'm afraid";
  }
}

sub ensure_directory {
  my $path = shift;
  if (-d $path) {
    return $path;
  } else {
    die "$path isn't a directory, I'm afraid";
  }
}

sub explain_mizar_err_file {
  my $err_file_path = ensure_existence (shift);
  my $err_file_basename = `basename $err_file_path`;
  chomp $err_file_basename;
  my $explanation = '';
  my $err_file_fh;
  my @errors = ();
  open $err_file_fh, q{<}, $err_file_path
    or die ("Unable to open input filehandle for $err_file_path: $!");
  while (defined (my $error_file_line = <$err_file_fh>)) {
    chomp $error_file_line;
    push @errors, $error_file_line;
  }
  close $err_file_fh;
  foreach my $error (@errors) {
    my ($line, $column, $code) = split (/\ /, $error);
    unless (defined $line && defined $column && defined $code) {
      die ("The error file line \"$error\" has an unexpected format");
    }
    $explanation .= pc_pad ("[$err_file_basename]: line $line, column $column: "
			    . mizar::lookup_error_code ($code));
  }
  return $explanation;
}

sub explain_err_files_in_directory {
  my $dir = ensure_directory (shift);
  my $explanation = '';
  my @non_trivial_err_files
    = `find $dir -type f -name "*\.err" -not -size 0 | xargs basename`;
  if (scalar @non_trivial_err_files == 0) {
    $explanation .= pc_pad ("No error files were generated by the mizar tools.");
  } else {
    $explanation .= pc_pad ("Summary of the errors in the mizar code:");
    $explanation 
      .= pc_pad ("************************************************************\n");
    foreach my $non_trivial_err_file (@non_trivial_err_files) {
      chomp $non_trivial_err_file;
      my $err_file_path = $dir . "/" . $non_trivial_err_file;
      $explanation .= pc_pad (explain_mizar_err_file ($err_file_path));
    $explanation
      .= pc_pad ("************************************************************\n");
    }
  }
  return $explanation;
}

sub explain_make_errors {
  my $dir = ensure_directory (shift);
  my $error_file_to_test = ensure_existence (shift);
  my $error_msg = shift;
  my $explanation = '';
  $explanation .= pc_pad ("$error_msg");
  $explanation .= pc_pad ("The output of the build procedure was:");
  $explanation .= pc_pad ("============================================================");
  open my $err_fh, q{<}, $error_file_to_test;
  while (defined (my $err_line = <$err_fh>)) {
    $explanation .= pc_pad ($err_line);
  }
  close $err_fh
    or die "Unable to close the input filehandle for $error_file_to_test! $!";
  $explanation .= pc_pad ("============================================================");
  $explanation .= explain_err_files_in_directory ($dir);
  return $explanation;
}

sub maybe_die_if_make_errors_in_dir {
  my $dir = ensure_directory (shift);
  my $error_file = ensure_existence (shift);
  my $error_message = shift;
  unless (-z $error_file) {
    pc_print (explain_make_errors ($dir, $error_file, $error_message));
    pc_print ('Your changes are inadmissible.');
    exit 1;
  }
  return;
}

sub die_if_make_died {
  my $exit_code = shift;
  unless ($exit_code == 0) {
    pc_print ("The build procedure did not exit cleanly! (exit code exit_code)\n");
    exit 1;
  }
  return;
}

# Compute the new dependencies
$ENV{"MIZFILES"} = $sandbox;

chdir ($sandbox_mml);

# Ensure that the new articles do not have any "@proof" bits.
if($allow_skipped_proofs == 0)
{
    chdir ($sandbox_mml);
    foreach my $lib_changed (@lib_changed) 
    {
	my $article_miz = $lib_changed . '.miz';
	if (system ("grep '\@proof' $article_miz | grep --invert-match --max-count=1 '::'") == 0) 
	{
	    pc_print ("You are not allowed to omit proofs from articles in the MML.");
	    exit 1;
	}
    }
}


## section-based verification
if ( defined ($ENV{MW_SECTION}) && defined ($ENV{MW_FILE}) 
     && ($ENV{MW_SECTION}=~m/^([a-z]+\d+)_(\d+)_(\d+)$/ ))
{
    my $section = $1;

    if (!($ENV{MW_FILE}=~ m/^mml\/([a-z0-9_]+)[.]$article_ext$/ ))
    {
	pc_print "Something went wrong with section editing: $ENV{MW_SECTION}, $ENV{MW_FILE}.";
	exit 1;	    
    }
    
    my $mw_file = $1;
    # add the file part
    $section = $section . '_' . $mw_file;
 
    ### TODO: this is a constraint on article naming, it must not
    ### conflict with section names

    # we want to have things ordered by the mml order here
    # instead of cat deps do tail ommitting the first item (hidden) - no .fdeps for it now
    my $dollar = '$';
    my @section_deps =
	`tail -n +2 deps | sed -e 's/$dollar/.fdeps/' | xargs egrep -l ' $section( |$dollar)' | sed -e 's/.fdeps$dollar//'`;

    my @articles_to_verify = ();
    my @articles_to_export = ();
    my @articles_to_html = ();


    # foreach my $todo ($mw_file, @section_deps) 
    # { 
    # 	system("touch mml/$todo.miz"); 
    # 	push(@articles_to_verify, 'mml/' . $todo . '.xmlvrf');
    # 	push(@articles_to_export, 'mml/' . $todo . '-prel');
    # 	push(@articles_to_html, 'mml/' . $todo . '.html');

    # }

    foreach my $todo ($mw_file, @section_deps) 
    {
	chomp($todo);
	system("touch $todo.miz"); 
	push(@articles_to_verify, $todo . '.xmlvrf');
	push(@articles_to_export, $todo . '-prel');
	push(@articles_to_html, $todo . '.html');
    }

    chdir ($sandbox);    

    system ("make  --jobs $makejobs " . join(' ', @articles_to_verify) . 
	    " 2> make-xml-err | tee make-xml-out");
    maybe_die_if_make_errors_in_dir ($sandbox_mml, "$sandbox/make-xml-err",
				     'Failure verifying and generating XML');

## Export the computed prel data
    system ("make --jobs $makejobs " . join(' ', @articles_to_export) 
	    . " > make-prel-out 2> make-prel-err | tee make-prel-out");

    maybe_die_if_make_errors_in_dir ($sandbox_mml, "$sandbox/make-prel-err",
				     'Failure exporting prel information');

    pc_print ("Your changes are admissible\n");

    system ("make --jobs $makejobs " . join(' ', @articles_to_html) 
	    . " WIKIHOST=$wikihost GITPROJECT=$repo_name.git 2> make-htmls-err | tee make-htmls-out");

# touch things now so they are clean for normal make
    system ("make touch-dependencies");

# Copy the sandbox: force compiled to be a copy of the known compiled MML.
# We are hoping that this is fairly fast.
    `rsync -a --del $sandbox/ $compiled`;
# note the slash   ===> ^ <===  see the rsync man page to learn why

    exit 0; # i.e., we made it -- phew!
   
}



system ("make  --jobs $makejobs evls deps 2> make-deps-err | tee make-deps-output");
# die_if_make_died ($? >> 8);
maybe_die_if_make_errors_in_dir ($sandbox_mml, 'make-deps-err',
				'Failure generating dependencies');

# Now try to verify the new articles (i.e., generate the article XML).
chdir ($sandbox);
system ("make  --jobs $makejobs hdrs xmlvrfs 2> make-xml-err | tee make-xml-out");
# die_if_make_died ($? >> 8);
maybe_die_if_make_errors_in_dir ($sandbox_mml, "$sandbox/make-xml-err",
				 'Failure verifying and generating XML');

# Export the computed prel data
chdir ($sandbox); # just to be clear
system ("make --jobs $makejobs prels > make-prel-out 2> make-prel-err | tee make-prel-out");
# die_if_make_died ($? >> 8);
maybe_die_if_make_errors_in_dir ($sandbox_mml, "$sandbox/make-prel-err",
				 'Failure exporting prel information');

pc_print ("Your changes are admissible\n");

exit 0; # i.e., we made it -- phew!
